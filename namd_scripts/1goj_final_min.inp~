* Minimize PDB
*

!Set file parameters
!====================
set prot  1goj_mod_vac_rmwc_smin_ntrl
set protDir $HOME/ortho_1goj/proteins
set outDir  $HOME/ortho_1goj/output

! Read topology and parameter files
open read card unit 10 name $HOME/toppar/top_all27_prot_na.rtf
read  rtf card unit 10
close unit 10

open read card unit 20 name $HOME/toppar/par_all27_prot_na_atp.prm
read para card unit 20
close unit 20

! Read PSF and Coordinates
open read unit 10 card name @protDir/@prot.psf
read psf  unit 10 card

rewind unit 10

open read unit 10 card name @protDir/@prot.crd
read coor unit 10 card
close unit 10
!===================

! set up SHAKE again
shake bonh param sele all end

!==============================
!Check coordinate statistics for crystal size and PME
coor stat sele all end

calc XSIZ = INT(  ?XMAX - ?XMIN )      
calc YSIZ = INT(  ?YMAX - ?YMIN )
calc ZSIZ = INT(  ?ZMAX - ?ZMIN )
!--------------------------------

crystal define orthorombic @XSIZ @YSIZ @ZSIZ 90.0 90.0 90.0
crystal build cutoff 14.0
!==============================

! These segments are used for water and ions in bulk solvent
define bulks sele segid WAT .or. segid POT .or. segid CL  end

! You may need to change these depending on how you plan to do recentering
image byseg sele .not. resname tip3 .and. .not. bulks end
image byres sele resname tip3 .or. bulks end


! the total charge on the system must be 0 or else Ewald will not work correctly, we need to test for this...
! we do the almost equal if test to avoid floating point rounding errors.
scalar charge stat sele all end
if ?stot .ae. 0 then goto okewald

! uh-oh, total charge != 0, we cannot proceed, set the bomlev to an absurdly high level
! to force a bailout.
bomlev 5
Achtung, particle-mesh ewald only works correctly on a structure with 0 total charge!!!!

label okewald

! set up nonbond correctly to the same values we will be using in dynamics

nbond inbfrq -1 imgfrq -1 -
      elec ewald pmew fftx 108 ffty 72 fftz 72 kappa .34 spline order 6 -
      vdw vswitch cutnb 16. cutim 16. ctofnb 12. ctonnb 10.

energy

coor copy comp

mini sd nstep 200
mini abnr nstep 1000 nprint 100 tolg 0.01
coor rms select type CA end

ioform extended

write psf card name @protDir/@prot_final.psf
* 1goj after final minimization
*

write coor card name @protDir/@prot_final.crd
* Final minimized coordinates, done with PME
*

stop

